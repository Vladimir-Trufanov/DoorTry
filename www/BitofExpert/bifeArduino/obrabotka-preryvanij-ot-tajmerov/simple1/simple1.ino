
/** Arduino UNO ******************************************* *** simple1.ino ***
 *  (based on timer interrupts by Amanda Ghassaei June 2012)
 * 
 * Обеспечить мигание светодиода с интервалом в 1 сек (1Гц) на плате через 
 * прерывания 16-тиразрядного первого таймера - Timer1 по событию совпадения 
 * с регистром сравнения (то есть в режиме CTC)
 * 
 * v1.2, 21.03.2024                                   Автор:      Труфанов В.Е.
 * Copyright © 2024 tve                               Дата создания: 20.03.2024
**/

// Инициируем переключатель светодиода - false
boolean toggle1 = 0;

void setup()
{
  pinMode(13, OUTPUT);
  cli(); // остановили прерывания
  
  // Очищаем биты регистров таймера
  TCCR1A = 0; // сбросили биты первого конфигурационного регистра
  TCCR1B = 0; // сбросили биты второго конфигурационного регистра
  TCNT1  = 0; // сбросили счетный регистр
  
  // Замечание: для использования режима СТС с целью получения интервала в одну 
  //    секунду нужно определить два значения для размещения их в 
  // регистрах таймера -
  //    предделитель подбираем равным 1024, что обеспечивает изменение 
  // счетчика (счетного регистра) не по каждому такту контроллера, а по 
  // прошествии 1024 тактов;
  //    значение счетчика определяем следующим образом за несколько шагов:
  
  // 1. Разрешение таймера = 1 / (тактовая частота контроллера / предделитель)
  
  // 2. Значение счетчика = интервал таймера / разрешение таймера - 1
  // (дополнительная единица добавляется к счетчику потому, что при совпадении 
  // счетчика с заданным значением он сбросится в ноль. Сброс занимает один 
  // тактовый период, который надо учесть в расчетах. Во многих случаях ошибка 
  // в один период не слишком значима, но в высокоточных задачах она может быть 
  // критичной.)

  // Разрешение таймера = 1 / (16000000 / 1024) = 0,000064 (cек)
  // Значение счетчика  = 1 / 0,000064 - 1      = 15624
   
  // Устанавливаем значение регистра сравнения для частоты на прерывание 1Гц
  OCR1A = 15624; // 1/(16000000/1024)-1 (должно быть <= 65535 = 2^16 степени-1)
  // Устанавливаем CTC режим для прерывания
  TCCR1B |= (1 << WGM12);
  // Устанавливаем биты для получения значения предделителя (prescaler) равного 1024 
  TCCR1B |= (1 << CS12) | (1 << CS10);  
  // Разрешаем прерывание первого таймера (канал А) по сравнению - СТС
  TIMSK1 |= (1 << OCIE1A);
  sei(); // разрешили прерывания
}

/** 
 * Обработать прерывание по сравнению от первого счетчика на канале А
 * с частотой 1 Гц
**/
ISR(TIMER1_COMPA_vect)
{
  // Если переключатель включен, то переводим пин светодиода в
  // верхний уровень и сбрасываем переключатель
  if (toggle1)
  {
    digitalWrite(13,HIGH);
    toggle1 = 0;
  }
  // Если переключатель ВЫКЛ, то переводим пин светодиода в
  // НИЖНИЙ уровень и устанавливаем переключатель
  else
  {
    digitalWrite(13,LOW);
    toggle1 = 1;
  }
}

void loop()
{
}

// ************************************************************ simple1.ino ***

