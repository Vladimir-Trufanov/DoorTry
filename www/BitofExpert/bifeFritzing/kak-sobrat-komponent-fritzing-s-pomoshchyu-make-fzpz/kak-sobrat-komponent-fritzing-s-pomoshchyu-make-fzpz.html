<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <title>Untitled</title>
  <style type="text/css">
      code{white-space: pre-wrap;}
      span.smallcaps{font-variant: small-caps;}
      span.underline{text-decoration: underline;}
      div.line-block{white-space: pre-line;}
      div.column{display: inline-block; vertical-align: top; width: 50%;}
  </style>
  <!--[if lt IE 9]>
    <script src="//cdnjs.cloudflare.com/ajax/libs/html5shiv/3.7.3/html5shiv-printshiv.min.js"></script>
  <![endif]-->
</head>
<body>
<h3 id="как-собрать-компонент-fritzing-с-помощью-make-fzpz-2024-02-25"><a href="https://github.com/Vladimir-Trufanov/BitofExpert/blob/main/bifeFritzing/kak-sobrat-komponent-fritzing-s-pomoshchyu-make-fzpz/kak-sobrat-komponent-fritzing-s-pomoshchyu-make-fzpz.md">Как собрать компонент Fritzing с помощью make-fzpz</a> [2024-02-25]</h3>
<p>При работе со Fritzing может случится так, что для проекта (или, например, для передачи другому пользователю) вам нужен готовый компонент, а у вас его нет, но есть его составляющие файлы изображений <strong><em>.svg</em></strong> и файл структуры компонента <strong><em>.fzp</em></strong>. В этом случае можно воспользоваться командным файлом <strong><em>make-fzpz.bat</em></strong>.</p>
<p>Для того чтобы понять, как это можно сделать, разберем пример сборки компонента USB UART преобразователя для программирования плат Arduino: <strong><em>USB-UART-CP2102-dtr.fzpz</em></strong>.</p>
<p><img src="USB-UART-CP2102-dtr.jpg" /></p>
<p>Для сборки компонента Fritzing с помощью bat-файла <strong><em>make-fzpz.bat</em></strong> нужно:</p>
<p>в рабочем (текущем) каталоге разместить:</p>
<p><strong><em>make-fzpz.bat</em></strong> - командный файл для сборки компонента Fritzing;</p>
<p><strong><em>7z.exe</em></strong> - исполняемый файл для архивирования файлов и каталогов.</p>
<p>Создать в рабочем каталоге подкаталог <strong><em>part</em></strong> и поместить в него файл метаданных компонента: <strong><em>USB-UART-CP2102-dtr.fzp</em></strong>.</p>
<p>Создать в рабочем каталоге подкаталог <strong><em>svg</em></strong> для размещения в нем svg-файлов различных представлений компонента: на макетной плате, разъёмов на печатной плате, электронной схемы компонента и его иконки.</p>
<p>Для каждого представления компонента создать свои собственные подкаталоги внутри каталога svg: <strong><em>breadboard</em></strong>, <strong><em>icon</em></strong>, <strong><em>pcb</em></strong> и <strong><em>schematic</em></strong>. К имени файла каждого представления добавить соответствующий постфикс _ <strong><em>breadboard</em></strong> , _ <strong><em>icon</em></strong>, _ <strong><em>pcb</em></strong> и _ <strong><em>schematic</em></strong> и разместить их в своих каталогах:</p>
<p>breadboard/USB-UART-CP2102-dtr_breadboard.svg,</p>
<p>icon/USB-UART-CP2102-dtr_icon.svg,</p>
<p>pcb/USB-UART-CP2102-dtr_pcb.svg,</p>
<p>schematic/USB-UART-CP2102-dtr_schematic.svg.</p>
<p>И окончательный шаг - открыть командную строку Windows и запустить командный файл, указав имя компонента следующим образом:</p>
<p><strong><em>make-fzpz USB-UART-CP2102-dtr</em></strong>.</p>
<p>После работы командного файла в текущем каталоге сформируется пакетный файл компонента <strong><em>USB-UART-CP2102-dtr.fzpz</em></strong> (сборка файла сопровождается интуитивно понятным диалогом).</p>
<p><img src="Structura-katalogov.jpg" /></p>
<h3 id="код-командного-файла">Код командного файла</h3>
<pre><code>@ECHO OFF
REM ************************************************************************
REM make-fzpz              Собрать компонент *.fzpz для загрузки во Fritzing 
REM Authors: Mike Pilgrem / Vladimir Trufanov
REM Remark:  так как история bat-файлов уходит в MS-DOS, то данный файл
REM          должен быть сохранён в кодировке OEM Russian (866)
REM ************************************************************************

REM Можно попросить помощь по работе с бат-файлом (три способа):  
REM   make-file /?
REM   make-file -h
REM   make-file --help

REM Для сборки компонента следует в качестве параметра указать имя компонента
REM   make-file detail

REM Если требуется помощь, уходим на вывод текста помощи
IF &quot;%1&quot; == &quot;/?&quot; GOTO HELP
IF &quot;%1&quot; == &quot;-h&quot; GOTO HELP
IF &quot;%1&quot; == &quot;--help&quot; GOTO HELP

REM Если в подкаталоге &quot;part&quot; файл метаданных детали &quot;detail.fzp&quot; 
REM отсутствует или отсутствует каталог, 
REM то выдаем предупреждение и завершаем работу

IF NOT EXIST part\%1.fzp (
    ECHO Файл метаданных детали part\%1.fzp не найден.
    GOTO END
)

REM Если в подкаталоге &quot; svg\icon&quot; файл иконки детали &quot;detail_icon.svg&quot; 
REM отсутствует, то выдаем предупреждение и завершаем работу

IF NOT EXIST svg\icon\%1_icon.svg (
    ECHO Ожидался файл иконки детали: svg\icon\%1_icon.svg, но не найден
    GOTO END
)

REM Если в подкаталоге &quot; svg\breadboard&quot; отсутствует файл макетного
REM представления детали &quot;detail_breadboard.svg&quot;, 
REM то выдаем предупреждение и завершаем работу

IF NOT EXIST svg\breadboard\%1_breadboard.svg (
    ECHO Не найден файл макетного представления детали: 
    ECHO svg\breadboard\%1_breadboard.svg.
    GOTO END
)

REM Проверяем представление детали в электронной схеме

IF NOT EXIST svg\schematic\%1_schematic.svg (
    ECHO Не найден файл представления детали в электронной схеме: 
    ECHO svg\schematic\%1_schematic.svg.
    GOTO END
)

REM Проверяем представление детали на печатной плате

IF NOT EXIST svg\pcb\%1_pcb.svg (
    ECHO Не найден файл представления детали на печатной плате: 
    ECHO svg\pcb\%1_pcb.svg.
    GOTO END
)

REM Предупреждаем о наличии прежней версии файла компонента &quot;detail.fzpz&quot;
REM и удаляем его при подтверждении запроса

IF EXIST %1.fzpz (
    ECHO Файл детали %1.fzpz уже существует.
    ECHO Он будет удален, если Вы продолжите.
    CHOICE /N /M &quot;Вы хотите продолжить (Y/N)?&quot;
    IF ERRORLEVEL 2 GOTO END
    DEL %1.fzpz
)

REM Делаем копии файлов для складывания их в архив детали

COPY part\%1.fzp part.%1.fzp &gt;NUL
COPY svg\icon\%1_icon.svg svg.icon.%1_icon.svg &gt;NUL
COPY svg\breadboard\%1_breadboard.svg svg.breadboard.%1_breadboard.svg &gt;NUL
COPY svg\schematic\%1_schematic.svg svg.schematic.%1_schematic.svg &gt;NUL
COPY svg\pcb\%1_pcb.svg svg.pcb.%1_pcb.svg &gt;NUL

REM Складываем файлы в zip-архив детали &quot;detail.fzpz&quot;
7z a -tzip %1.fzpz part.%1.fzp svg.icon.%1_icon.svg svg.breadboard.%1_breadboard.svg svg.schematic.%1_schematic.svg svg.pcb.%1_pcb.svg &gt;NUL

REM Удаляем копии
DEL part.%1.fzp svg.icon.%1_icon.svg svg.breadboard.%1_breadboard.svg svg.schematic.%1_schematic.svg svg.pcb.%1_pcb.svg

REM Сообщаем, что файл детали создан
ECHO Новый файл детали для Fritzing: %1.fzpz создан.

GOTO END

:HELP
ECHO Для создания компонента Fritzing требуется наличие архиватора 7z.exe в
ECHO текущем каталоге или должен быть проложен путь к его расположению в Windows.
ECHO.
ECHO.  make-fzpz [/?^|-h^|--help] ^&lt;partname^&gt;
ECHO.
ECHO ^&lt;partname^&gt;       Имя детали во Fritzing. Не должно включать пробелов.
ECHO [/?^|-h^|--help]   Выводит это сообщение помощи.
ECHO.
ECHO Предполагается, что *.fzp и *.svg файлы размещены в каталогах
ECHO \part, \svg\icon, \svg\breadboard, \svg\schematic and \svg\pcb, с именами
ECHO ^&lt;partname^&gt;.fzp
ECHO ^&lt;partname^&gt;_^&lt;view^&gt;.svg
ECHO.
ECHO Где ^&lt;view^&gt; одно из четырех представлений: icon, breadboard, schematic or pcb.
ECHO.
:END

REM ------------------------------------------------------------------------
REM Copyright (c) 2015, Mike Pilgrem
REM All rights reserved.

REM Redistribution and use in source and binary forms, with or without
REM modification, are permitted provided that the following conditions are met:

REM 1. Redistributions of source code must retain the above copyright notice,
REM    this list of conditions and the following disclaimer.

REM 2. Redistributions in binary form must reproduce the above copyright notice,
REM    this list of conditions and the following disclaimer in the documentation
REM    and/or other materials provided with the distribution.

REM THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS &quot;AS IS&quot;
REM AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
REM IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
REM ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
REM LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
REM CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
REM SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
REM INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
REM CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
REM ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
REM POSSIBILITY OF SUCH DAMAGE.
REM ------------------------------------------------------------------------</code></pre>
<h3 id="библиография">Библиография</h3>
<h3 id="make-fzpz-windows-tools-to-create-a-fritzing-part-bundle-file"><a href="https://github.com/mpilgrem/make-fzpz">make-fzpz: Windows tools to create a Fritzing part bundle file</a></h3>
<pre><code>Fritzing позволяет пользователям документировать прототипы своей электроники, в том числе путем создания собственных деталей.
Цель этого проекта - предоставить инструмент Windows для создания архивного файла *.fzpz из файла метаданных детали (*.fzp) и связанных с ним файлов векторной графики (*.svg).
Инструмент предполагает, что файл метаданных находится в подкаталоге \part, а файлы векторной графики - в подкаталогах подкаталога \svg.
make-fzpz.bat - это командный файл Windows. Он требует, установленного пути к  приложению 7z.exe.
</code></pre>
</body>
</html>
